import win.ui;
import process;
import process.popen;
import string;
import fsys.ini;
import fsys;
import fonts.fontAwesome;
/*DSG{{*/
var frmTab1 = win.form(text="挂载";right=399;bottom=499;max=false)
frmTab1.add(
btn_mount={cls="button";text="挂载";left=240;top=64;right=312;bottom=104;dl=1;dt=1;font=LOGFONT(h=-21);z=1};
btn_netMount={cls="button";text="映射";left=240;top=208;right=312;bottom=248;dl=1;dt=1;font=LOGFONT(h=-21);z=15};
btn_openMountPath={cls="button";text="打开";left=320;top=64;right=392;bottom=104;disabled=1;dl=1;dt=1;font=LOGFONT(h=-21);z=20};
btn_showPwd={cls="button";text='\uF06E';left=232;top=296;right=256;bottom=320;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=19};
cbb_driveList={cls="combobox";left=152;top=80;right=194;bottom=106;border=1;edge=1;font=LOGFONT(h=-16);items={"H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z"};mode="dropdown";z=3};
cbb_sections={cls="combobox";left=152;top=16;right=312;bottom=42;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-24);items={};mode="dropdownlist";z=18};
cbb_serverPort={cls="combobox";left=120;top=216;right=224;bottom=242;dt=1;edge=1;font=LOGFONT(h=-16);items={"9990","12222"};mode="dropdown";z=10};
cbb_serverType={cls="combobox";left=120;top=176;right=224;bottom=202;dt=1;edge=1;font=LOGFONT(h=-16);items={"webdav","ftp","sftp","http"};mode="dropdownlist";z=8};
edt_LogShow={cls="edit";left=16;top=384;right=384;bottom=488;edge=1;hscroll=1;multiline=1;readonly=1;vscroll=1;z=17};
edt_serverPwd={cls="edit";left=120;top=296;right=224;bottom=322;dt=1;edge=1;font=LOGFONT(h=-16);password=1;z=14};
edt_serverUser={cls="edit";left=120;top=256;right=224;bottom=282;dt=1;edge=1;font=LOGFONT(h=-16);multiline=1;z=13};
groupbox={cls="groupbox";text="网络映射到：";left=8;top=136;right=392;bottom=344;edge=1;font=LOGFONT(h=-21);z=6};
groupbox3={cls="groupbox";text="执行结果/Logs";left=8;top=352;right=392;bottom=496;edge=1;font=LOGFONT(h=-21);z=16};
static={cls="static";text="本地挂载到：";left=16;top=80;right=152;bottom=112;font=LOGFONT(h=-21);notify=1;transparent=1;z=2};
static2={cls="static";text="盘";left=208;top=80;right=240;bottom=112;aw=1;font=LOGFONT(h=-21);transparent=1;z=4};
static3={cls="static";text="当前操作库：";left=16;top=24;right=152;bottom=56;dl=1;dt=1;font=LOGFONT(h=-21);notify=1;transparent=1;z=5};
static4={cls="static";text="映射模式：";left=24;top=176;right=128;bottom=208;dl=1;dt=1;font=LOGFONT(h=-19);transparent=1;z=7};
static5={cls="static";text="端口：";left=24;top=216;right=128;bottom=248;dl=1;dt=1;font=LOGFONT(h=-19);transparent=1;z=9};
static6={cls="static";text="密码：";left=24;top=296;right=128;bottom=328;dl=1;dt=1;font=LOGFONT(h=-19);transparent=1;z=11};
static7={cls="static";text="用户名：";left=24;top=256;right=128;bottom=288;dl=1;dt=1;font=LOGFONT(h=-19);transparent=1;z=12}
)
/*}}*/

var isMounted = 0 //是否挂载
var isNetMounted = 0; //是否网络映射
var currentPrcsId = 0;   // 存储挂载 PID
var currentNetPrcsId = 0;// 存储映射 PID
var prcs = null;         // 挂载进程对象
var netPrcs = null;      // 映射进程对象
var vaultName_Mounted = "";
var vaultName_NetMounted = "";

if(frmTab1.cbb_sections.count==0){
	frmTab1.cbb_sections.addItems(..sectionNames);
	frmTab1.cbb_sections.selIndex = 1;
}

var vaultName = frmTab1.cbb_sections.selText;

// 本地挂载的按钮
frmTab1.btn_mount.oncommand = function(id,event){
	vaultName = frmTab1.cbb_sections.selText;
	var ds = frmTab1.cbb_driveList.text;
	var ds_cmd = ds + ":";
	if(isMounted==0 and fsys.path.isDir(ds_cmd)){
		frmTab1.msgboxErr("已存在该分区，请更换其他分区");
		return false; 
		}
	if(isMounted==0){
		vaultName_Mounted = vaultName;
		var vaultName_cmd = vaultName + ":";
		var t1 = "将库 " ++ vaultName ++ " 挂载在" ++ ds ++ "盘";
		// 用数组传参，由 aardio 自动处理空格和引号
		// dir-cache-time defualt 5min
		var args = {
        	"mount"; vaultName_cmd; ds_cmd;
        	"--vfs-cache-mode"; "full";
        	"--dir-cache-time"; "60s";
        	"--links"
    	};
		prcs_cmd = process.joinArguments(..rclone_Path,args); 
		prcs = process.popen(..rclone_Path,args);
		if(!prcs){
        	TextLog("错误: 无法启动 Rclone 进程");
        	return;
    	}
		prcs.codepage = 65001; // 关键步骤：强制指定进程输出编码为 UTF-8 (65001)
		prcs.killOnExit(); // 确保主程序退出时，挂载也强制卸载
    	currentPrcsId = prcs.process.id; // 记录下这个进程的 PID，用于后续精确关闭
		TextLog("程序执行命令为： " + prcs_cmd);
		TextLog(t1);
		TextLog("本地挂载进程PID为：" + tostring(currentPrcsId));
		prcs.logResponse(frmTab1.edt_LogShow);
		frmTab1.btn_mount.text = "停止";
		for(i=1;3;1){
			thread.delay(500);
			if(fsys.path.isDir(ds_cmd)){
				//frmTab1.cbb_sections.disabled = true;
				frmTab1.cbb_driveList.disabled = true;
				frmTab1.btn_openMountPath.disabled = false;
				isMounted = 1;
				break;
			}
		}

	}
	elseif(isMounted == 1){
    	frmTab1.btn_mount.disabledText = "停止中";
    	
    	// 1. 优先尝试正常卸载逻辑 (Windows 下推荐使用 taskkill 发送关闭信号而非直接 terminate)
    	if(prcs){
        	killProcessById(currentPrcsId);
        	// 给一点缓冲时间让系统回收盘符
        	for(i=1;10;1){
            	thread.delay(300);
            	if(!fsys.path.isDir(ds_cmd)) break; // 检查盘符是否已消失
           		if(i == 10 && prcs) prcs.terminate(); // 如果 3秒后还没卸载，尝试对象级别的终结
        	}
    	}
    	
    	// 2. 二次检查：如果盘符依然残留，尝试强力清除
    	if(fsys.path.isDir(ds_cmd)){
        	// 强制结束进程对象
        	if(prcs) prcs.terminate();
        	// 针对 WinFsp 的残留，有时需要重启资源管理器或手动卸载（可选高级操作）
        	TextLog("警告：盘符卸载缓慢，请检查资源管理器。必要时重启资源管理器。");
    	}
	
    	// 3. 重置 UI 状态
    	frmTab1.btn_mount.disabledText = null;
    	frmTab1.cbb_driveList.disabled = false;
    	frmTab1.btn_openMountPath.disabled = true;
    	isMounted = 0;
    	prcs = null; // 释放对象
    	TextLog("已停止库 " ++ vaultName_Mounted ++ " 的本地挂载");
    	TextLog("-------------------------------------------");
    	frmTab1.btn_mount.text = "挂载";
	}
}

// 网络映射的按钮
frmTab1.btn_netMount.oncommand = function(id,event){
	vaultName = frmTab1.cbb_sections.selText;
	var vaultName_cmd = vaultName + ":";
	var nm_serverType = frmTab1.cbb_serverType.selText;
	var nm_serverPort = frmTab1.cbb_serverPort.selText;
	var nm_serverUser = frmTab1.edt_serverUser.text;
	var nm_serverPwd = frmTab1.edt_serverPwd.text;
	var t1 = "将库 " ++ vaultName ++ " 通过 " ++  nm_serverType ++ " 方式映射在 0.0.0.0:" ++ nm_serverPort;
	var t2;
	if(nm_serverPwd !="" and nm_serverUser ==""){
		frmTab1.msgboxErr("若设置了密码，需要同时设置用户名" ++ '\r\n' ++ "或将密码清空","密码无效");
		return ; 	
	}
	if(nm_serverUser!=""){
		t2 = "用户名user: " ++ nm_serverUser ++ "  密码pwd: " ++ nm_serverPwd;	
	}
	else{
		t2 = "无用户名和密码;使用匿名登录";
	}
	var t3 = "通过支持协议 " ++ nm_serverType ++ " 的浏览器或管理器访问 127.0.0.1:" ++ nm_serverPort ++ " 以查看映射后的加密库";
	if(isNetMounted == 1){
		try{
			if(netPrcs){
				netPrcs.terminate();
			}
		}
		isNetMounted = 0;
		netPrcs = null;
		frmTab1.btn_netMount.text = "映射";
		frmTab1.cbb_serverType.disabled = false;
		frmTab1.cbb_serverPort.disabled = false;
		frmTab1.edt_serverUser.readonly = false;
		frmTab1.edt_serverPwd.readonly = false;
		TextLog("已停止库 " ++ vaultName_NetMounted ++ " 的网络映射");	
		TextLog("-------------------------------------------");
			
	}
	elseif(isNetMounted == 0){
		var args = {"serve"; nm_serverType; vaultName_cmd; "--addr"; "0.0.0.0:" + nm_serverPort};
    	if(#nm_serverUser){
    		table.push(args, "--user", nm_serverUser, "--pass", nm_serverPwd);
    	}
		netPrcs_cmd = process.joinArguments(..rclone_Path,args);
		netPrcs,netPrcsErr = process.popen(..rclone_Path,args);
		if(!netPrcs) { 
			TextLog("错误: 网络服务映射失败 或 无法启动 Rclone 进程");
			TextLog("-------------------------------------------");
			return frmTab1.msgboxErr(netPrcsErr); 
		}
		else{
			currentNetPrcsId = netPrcs.process.id;
			netPrcs.killOnExit(); // 确保主程序退出时，挂载也强制卸载
			netPrcs.codepage = 65001; // 关键步骤：强制指定进程输出编码为 UTF-8 (65001)
			TextLog("程序执行命令为： " + netPrcs_cmd);
			isNetMounted = 1;
			vaultName_NetMounted = vaultName;
			frmTab1.btn_netMount.text = "停止";
			frmTab1.cbb_serverType.disabled = true;
			frmTab1.cbb_serverPort.disabled = true;
			frmTab1.edt_serverUser.readonly = true;
			frmTab1.edt_serverPwd.readonly = true;
			TextLog(t1);
			TextLog(t2);
			TextLog(t3);
			TextLog("网络映射进程PID为：" + tostring(currentNetPrcsId));
			netPrcs.logResponse(frmTab1.edt_LogShow);
		}
	}
}

TextLog = function(text){
	frmTab1.edt_LogShow.appendText("[ " ++ NowTime() ++ "] " ++ tostring(text) ++ '\r\n');
	frmTab1.edt_LogShow.scrollCaret();
}

NowTime = function(){
	var tm = tostring(time()) + " ";
	return tm;
}

// 静默结束进程（精确 PID 模式）
killProcessById = function(pid){
    if(!pid) return;
    // 使用隐藏模式执行 taskkill，避免黑框
    process.execute("taskkill", string.format("/F /T /PID %d", pid), 0); 
}

// 辅助功能：显示/隐藏密码
frmTab1.btn_showPwd.oncommand = function(id,event){
	if(frmTab1.edt_serverPwd.passwordChar){
		frmTab1.edt_serverPwd.passwordChar = null;
		frmTab1.btn_showPwd.text = '\uF070'; //"隐"
	}
	else{
		frmTab1.edt_serverPwd.passwordChar = "*";
		frmTab1.btn_showPwd.text = '\uF06E' ; //"显"
	}
}

// 辅助功能：打开盘符
frmTab1.btn_openMountPath.oncommand = function(id,event){
	var ds = frmTab1.cbb_driveList.selText ++ ":";
	if(fsys.path.isDir(ds)){
        process.execute("explorer.exe", ds);
    }
}

// 窗口关闭前的最终清理
frmTab1.onClose = function(hwnd,message,wParam,lParam){
	try{
		if(currentPrcsId && currentPrcsId != 0 ) killProcessById(currentPrcsId);
        if(currentNetPrcsId && currentNetPrcsId != 0 ) killProcessById(currentNetPrcsId);
        // 给系统一点响应时间
        thread.delay(400);
        // 最终强行清理对象
        if(prcs) prcs.terminate();
        if(netPrcs) netPrcs.terminate();
	}
}

frmTab1.show();
win.loopMessage();
return frmTab1;